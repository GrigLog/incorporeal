buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "com.wynprice.cursemaven:CurseMaven:2.1.1"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.wynprice.cursemaven"

version = "1.2.3.1"
ext.apiVersion = 3
group = "quaternary.incorporeal"
archivesBaseName = "incorporeal"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

//coremod SPOOK
minecraft.coreMod = "quaternary.incorporeal.spookyasm.IncorporealPlugin"

jar {    
    manifest {
        attributes "FMLCorePlugin": "quaternary.incorporeal.spookyasm.IncorporealPlugin"
        attributes "FMLCorePluginContainsFMLMod": true
        attributes "FMLAT": "incorporeal_at.cfg"
    }
    
    from sourceSets.main.output
    from sourceSets.api.output
}

repositories {
    maven {
        url "http://maven.thiakil.com"
    }
    
    maven {
        url "http://maven.blamejared.com"
    }
    
    maven {
        url "https://minecraft.curseforge.com/api/maven"
    }
    
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
}

dependencies {
    //bubbles
    deobfCompile "com.azanor.baubles:Baubles:1.12-1.5.2"
    //botania
    deobfCompile "curse.maven:Botania:2782181"
    
    //(soft dep) infraredstone (from curse maven)
    deobfCompile "infraredstone:InfraRedstone:1.2.1"
    
    //(soft dep) crafttweaker thingie
    deobfCompile "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.15.527"
    
    //(soft dep) jei
    deobfProvided "mezz.jei:jei_1.12.2:4.15.0.268:api"
    runtime "mezz.jei:jei_1.12.2:4.15.0.268"
    
    //runtime curse.resolve("tool-progression", 2693458)
}

minecraft {
	version = "1.12.2-14.23.5.2768"
	runDir = "run"

	mappings = "stable_39"

	useDepAts = true
	makeObfSourceJar = false

	if (project.hasProperty("mc_uuid")) {
		clientRunArgs += ["--uuid", project.mc_uuid]
	}

	if (project.hasProperty("mc_username")) {
		clientRunArgs += ["--username", project.mc_username]
	}

	replace "GRADLE:VERSION", project.version
	replace "GRADLE:API-VERSION", project.apiVersion
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}